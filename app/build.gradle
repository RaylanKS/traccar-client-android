// Note that changes are made for this file by Raylan Klitzke Schultz
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.mikepenz.aboutlibraries.plugin'

def version = '1.0.0'

aboutLibraries {
    includeAllLicenses = true
}

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId 'br.com.softquick.rastreio'
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 3
        versionName version
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES']
        }
    }


    flavorDimensions 'default'
    productFlavors {
        regular {
            isDefault = true
        }
        google {
            versionName version + "-google"
        }
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            debuggable false
            jniDebuggable false
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    lint {
        checkReleaseBuilds false
    }
    buildToolsVersion '31.0.0'
}

dependencies {
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.test:core-ktx:1.4.0'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'com.github.judemanutd:autostarter:1.1.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.7.3'
    googleImplementation platform('com.google.firebase:firebase-bom:28.2.1')
    googleImplementation 'com.google.firebase:firebase-core:20.0.2'
    googleImplementation 'com.google.firebase:firebase-messaging:23.0.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    googleImplementation 'com.google.android.gms:play-services-location:19.0.1'
    googleImplementation 'com.google.android.play:core:1.10.3'
    implementation "androidx.fragment:fragment-ktx:1.4.0"
    implementation "com.mikepenz:aboutlibraries-core:8.9.4"
    implementation "com.mikepenz:aboutlibraries:8.9.4"
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'

}

if (getGradle().getStartParameter().getTaskRequests().toString().contains('Google')) {
    apply plugin: 'com.google.gms.google-services'

    task copyJson(type: Copy) {
        from '../../environment/google-services'
        into '.'
        include 'traccar-client-android.json'
        rename('traccar-client-android.json', 'google-services.json')
    }

    afterEvaluate {
        preBuild.dependsOn copyJson
    }
}
